plugins {
    id "com.jfrog.bintray" version "1.6"
}

group 'io.github.oneyx.qcloudim'
version '0.1.0'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

def var = version

dependencies {
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.39'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.58'
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.58'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

javadoc {
    options.encoding = "UTF-8"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.github.oneyx.qcloudim'
            artifactId 'qcloudim-sdk'
            version var
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = 'qcloudim-sdk'
        licenses = [ 'GPL-3.0' ]
        vcsUrl = 'https://github.com/OneYX/qcloudim-sdk.git'

        version {
            name =  var
            vcsTag = var
        }
    }

    publications = [ 'maven' ]
}